statement:
    - KEYWORD:return LPAREN EOL* statement? EOL* RPAREN
        -> Return
    - KEYWORD:break LPAREN EOL* RPAREN
        -> Break
    - KEYWORD:continue LPAREN EOL* RPAREN
        -> Continue
    - KEYWORD:include LPAREN EOL* statement EOL* RPAREN
        -> Continue
    - expr

expr:
    - KEYWORD:varcreate IDENTIFIER EQ expr
        -> VarCreate
    - compexpr ((KEYWORD:logicaland|KEYWORD:logicalor) compexpr)*
        -> BinaryOp

compexpr:
    - KEYWORD:logicalnot compexpr
        -> UnaryOp
    - arithexpr ((EQEQUALS|BANGEQUALS|LESSTHAN|LTEQUALS|GREATERTHAN|GTEQUALS) arithexpr)*
        -> BinaryOp

arithexpr:
    - term ((PLUS|DASH) term)*
        -> BinaryOp

term:
    - raised ((ASTRISK|FSLASH) raised)*
        -> BinaryOp

raised:
    - factor (CARAT factor)*
        -> BinaryOp

factor:
    - (PLUS|DASH) factor
        -> UnaryOp
    - atom calttricie*

atom:
    - INT
        -> Int
    - FLOAT
        -> Float
    - STRING
        -> String
    - LPAREN EOL* statement EOL* RPAREN
    - IDENTIFIER EQUALS expr
        -> VarAssign
    - IDENTIFIER
        -> VarAccess
    - arrayexpr
        -> Array
    - dictionaryexpr
        -> Dictionary
    - funcdef
        -> FuncCreate
    - handler
        -> Handler
    - ifexpr
        -> If
    - switchexpr:
        -> Switch
    - loopexpr
        -> ForLoop|WhileLoop

calttricie:
    - (LPAREN EOL* (expr (COMMA EOL* expr)*)? EOL* RPAREN)+
        -> VarCall
    - (LSQUARE EOL* statement EOL* RSQUARE)+
        -> Indicie
    - (EOL* PERIOD IDENTIFIER)+
        -> Attribute

arrayexpr:
    - LSQUARE EOL* (statement (COMMA EOL* statement)*)? EOL* RSQUARE
        -> Array

dictionaryexpr:
    - LCURLY EOL* (statement COLON EOL* statement (COMMA ROL* statement COLON EOL* statement)*)? EOL* RCURLY
        -> Array

funcdef:
    - KEYWORD:createfunc LPAREN EOL*
      (IDENTIFIER (COMMA EOL* IDENTIFIER)*)? EOL*
      RPAREN EOL* codeblock
        -> FuncCreate
    - KEYWORD:createlambda LPAREN EOL*
      (IDENTIFIER (COMMA EOL* IDENTIFIER)*)? EOL*
      RPAREN EOL* LCURLY EOL* statement EOL* RCURLY
        -> LambdaCreate

handler:
    - KEYWORD:handler EOL* codeblock
        -> Handler

ifexpr:
    - KEYWORD:if LPAREN EOL* statement EOL* RPAREN EOL* codeblock EOL*
      (KEYWORD:elif LPAREN EOL* statement EOL* RPAREN EOL* codeblock EOL*)*
      (KEYWORD:else EOL* codeblock)?
        -> If

switchexpr:
    - KEYWORD:case LPAREN EOL* KEYWORD:varcreate? IDENTIFIER KEYWORD:in statement EOL* RPAREN EOL* LCURLY EOL*
      (KEYWORD:when LPAREN EOL* statement EOL* RPAREN EOL* codeblock EOL*)+
      (KEYWORD:else EOL* codeblock)? EOL*
      RCURLY
        -> Switch

loopexpr:
    - KEYWORD:forloop LPAREN EOL* KEYWORD:varcreate? IDENTIFIER KEYWORD:in statement EOL* RPAREN EOL* codeblock
        -> ForLoop
    - KEYWORD:whileloop LPAREN EOL* statement EOL* RPAREN EOL* codeblock
        -> WhileLoop

codeblock:
    - LCURLY EOL* (statement (EOL statement))? EOL* RCURLY